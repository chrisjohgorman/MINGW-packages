# Maintainer: Chris Gorman <chrisjohgorman@gmail.com>

_realname=pyerfa
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=2.0.1.1
pkgrel=1
pkgdesc="Bindings for ERFA routines(mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://pyerfa.readthedocs.io/'
license=(BSD)
depends=("${MINGW_PACKAGE_PREFIX}-erfa"
	"${MINGW_PACKAGE_PREFIX}-python"
	"${MINGW_PACKAGE_PREFIX}-python-numpy")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build" 
	     "${MINGW_PACKAGE_PREFIX}-python-installer"
	     "${MINGW_PACKAGE_PREFIX}-python-jinja"
	     "${MINGW_PACKAGE_PREFIX}-python-packaging"
	     "${MINGW_PACKAGE_PREFIX}-python-setuptools-scm"
	     "${MINGW_PACKAGE_PREFIX}-python-wheel")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pytest")
options=('!strip')
source=(https://files.pythonhosted.org/packages/source/p/${_realname}/${_realname}-${pkgver}.tar.gz)
sha256sums=('dbac74ef8d3d3b0f22ef0ad3bbbdb30b2a9e10570b1fa5a98be34c7be36c9a6b')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  sed 's/oldest-supported-//' -i pyproject.toml
  ## (OPTIONAL) Only if setuptools-scm is used
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}"
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  export PYERFA_USE_SYSTEM_LIBERFA=1

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  msg "Python test for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  sed -e '/addopts/d' -i setup.cfg

  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')

  cd build/lib.mingw_${CARCH}-cpython-${python_version}/erfa
# The test command will usually depend upon what is contained in the tox.ini file
# or in the [testenv:py] section of the pyproject.toml file.
  ${MINGW_PREFIX}/bin/python -m pytest
}

package() {
  msg "Python install for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  local test_directory=$(python -c 'import site; a=site.getsitepackages()[0].split("/")[2:]; b="/".join(a); print(b)')

  rm -r "${pkgdir}/${test_directory}/erfa/tests"
  install -Dm644 LICENSE.rst "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE.rst"
}
