# Maintainer: Chris Gorman <chrisjohgorman@gmail.com>

_realname=astropy
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=6.0.1
pkgrel=1
pkgdesc="Common core package for Astronomy (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://www.astropy.org/'
license=(BSD)
depends=("${MINGW_PACKAGE_PREFIX}-python"
	 "${MINGW_PACKAGE_PREFIX}-cfitsio"
	 "${MINGW_PACKAGE_PREFIX}-expat"
	 "${MINGW_PACKAGE_PREFIX}-wcslib"
	 "${MINGW_PACKAGE_PREFIX}-python-numpy"
	 "${MINGW_PACKAGE_PREFIX}-python-pyerfa"
	 "${MINGW_PACKAGE_PREFIX}-python-yaml"					
	 "${MINGW_PACKAGE_PREFIX}-python-packaging"						
	 "${MINGW_PACKAGE_PREFIX}-python-astropy-iers-data")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build" 
	     "${MINGW_PACKAGE_PREFIX}-python-installer"
	     "${MINGW_PACKAGE_PREFIX}-python-wheel"
	     "${MINGW_PACKAGE_PREFIX}-python-setuptools-scm"
	     "${MINGW_PACKAGE_PREFIX}-python-extension-helpers"
	     "${MINGW_PACKAGE_PREFIX}-python-tomli"
	     "${MINGW_PACKAGE_PREFIX}-cython")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pytest"
              "${MINGW_PACKAGE_PREFIX}-python-pytest-xdist"
              "${MINGW_PACKAGE_PREFIX}-python-hypothesis")
options=('!strip')
source=("https://files.pythonhosted.org/packages/source/a/${_realname}/${_realname}-${pkgver}.tar.gz"
        0001-fix_broken_compilation_with_gcc_14.patch
        0002-pthread_missing.patch)
sha256sums=('89a975de356d0608e74f1f493442fb3acbbb7a85b739e074460bb0340014b39c'
            '2f1e25b43839e0d780062639d8cb027c4069da2070c4f3bd8c2ae06e04959dea'
            '4b5b658cb78fc5513f1c102c7efa6fc6aee8a292a7c67531a97c6cdca9a73713')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -Np1 -i "${srcdir}/0001-fix_broken_compilation_with_gcc_14.patch"
  patch -Np1 -i "${srcdir}/0002-pthread_missing.patch"

  ## (OPTIONAL) Only if setuptools-scm is used
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}"
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  ASTROPY_USE_SYSTEM_ALL=1 \
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  msg "Python test for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python setup.py build_ext --inplace

# The test command will usually depend upon what is contained in the tox.ini file
# or in the [testenv:py] section of the pyproject.toml file.
  ${MINGW_PREFIX}/bin/python -m pytest || warning "Tests failed"
}

package() {
  msg "Python install for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"

  ASTROPY_USE_SYSTEM_ALL=1 \
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE.rst "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE.rst"
}
