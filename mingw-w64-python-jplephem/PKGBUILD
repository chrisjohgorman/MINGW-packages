# Maintainer: Chris Gorman <chrisjohgorman@gmail.com>

_realname=jplephem
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=2.22
pkgrel=1
pkgdesc="Use a JPL ephemeris to predict planet positions (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/brandon-rhodes/python-jplephem'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build" 
	     "${MINGW_PACKAGE_PREFIX}-python-installer"
	     "${MINGW_PACKAGE_PREFIX}-python-setuptools"
	     "${MINGW_PACKAGE_PREFIX}-python-wheel"
)
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-numpy"
              "${MINGW_PACKAGE_PREFIX}-python-setuptools-scm")
options=('!strip')
source=("https://files.pythonhosted.org/packages/source/j/${_realname}/${_realname}-${pkgver}.tar.gz"
        "https://github.com/brandon-rhodes/python-jplephem/raw/master/ci/de405.bsp"
        "https://github.com/brandon-rhodes/python-jplephem/raw/master/ci/de421.bsp"
        "https://github.com/brandon-rhodes/python-jplephem/raw/master/ci/moon_pa_de421_1900-2050.bpc")

sha256sums=('0d9acc7217b4806feba93e72974ceead5611104bce6789af38d4f27dcf7a592c'
            '30a7113793ee5b6bf1e5546c6dfc21d9682d9ffabfe9b17b4bab27ba2ac75c89'
            '08b20db2ae22488650641c5a9033e5bfda4b1c4b440cfeaf20f621cfa18ecdb3'
            '656f90616403d75a75f0cd6c8830fc5b44f8cb4facb5ccb8915e752b397520cf')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  cp "${srcdir}"/*de*b*p .

  ## (OPTIONAL) Only if setuptools-scm is used
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}"
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  msg "Python test for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"

# The test command will usually depend upon what is contained in the tox.ini file
# or in the [testenv:py] section of the pyproject.toml file.
  ${MINGW_PREFIX}/bin/python -m unittest || warning "Tests failed"
}

package() {
  msg "Python install for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE.txt -t "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}"
  install -Dm644 README.md "${pkgdir}${MINGW_PREFIX}/share/doc/python-${_realname}"
}
