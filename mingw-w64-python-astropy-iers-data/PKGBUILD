# Maintainer: Chris Gorman <chrisjohgorman@gmail.com>

_realname=astropy-iers-data
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.2024.03.18.00.29.47
pkgrel=1
pkgdesc="IERS Earth Rotation and Leap Second tables for the astropy core package (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/astropy/astropy-iers-data'
license=(BSD-3-Clause)
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
	     "${MINGW_PACKAGE_PREFIX}-python-installer"
	     "${MINGW_PACKAGE_PREFIX}-python-wheel"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools-scm")
options=('!strip')
source=("https://github.com/astropy/${_realname}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "python-astropy_0_2024_03_18_00_29_47-issue-1.patch::https://github.com/astropy/astropy-iers-data/pull/24.patch")
sha256sums=('348dad7dc09012e5abb490706c176ba3e4e384e68dbc505f5b7a79ca1a912940'
            '79ecf580efcf66bf16816ff47c0b044946c20aae04356677acf7fc3e96b98d82')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -Np1 -i "${srcdir}/python-astropy_0_2024_03_18_00_29_47-issue-1.patch"

  ## (OPTIONAL) Only if setuptools-scm is used
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}"
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  msg "Python install for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE.rst "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE.rst"
}
