# Maintainer: Chris Gorman <chrisjohgorman@gmail.com>

_realname=construct
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=2.10.70
pkgrel=1
pkgdesc="Powerful declarative parser/builder for binary data (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://construct.readthedocs.org/'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build" 
	     "${MINGW_PACKAGE_PREFIX}-python-installer"
	     "${MINGW_PACKAGE_PREFIX}-python-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-arrow"
	      "${MINGW_PACKAGE_PREFIX}-python-cloudpickle"
	      "${MINGW_PACKAGE_PREFIX}-python-cryptography"
	      "${MINGW_PACKAGE_PREFIX}-python-lz4"
	      "${MINGW_PACKAGE_PREFIX}-python-numpy"
	      "${MINGW_PACKAGE_PREFIX}-python-pytest"
	      "${MINGW_PACKAGE_PREFIX}-python-pytest-benchmark"
	      "${MINGW_PACKAGE_PREFIX}-python-ruamel-yaml")
optdepends=("${MINGW_PACKAGE_PREFIX}-python-arrow: for Timestamp"
	      "${MINGW_PACKAGE_PREFIX}-python-numpy: for arrat serialization with Numpy protocol"
	      "${MINGW_PACKAGE_PREFIX}-python-ruamel-yaml: for KaitaiStruct (KSY) eporter")
options=('!strip')
source=("${_realname}-${pkgver}.tar.gz::https://github.com/${_realname}/${_realname}/archive/v${pkgver}.tar.gz")
sha256sums=('ea468f7970fe425e2190016875a1f5955d45753d4560e84c5921e8d575b237c2')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ## (OPTIONAL) Only if setuptools-scm is used
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}"
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  msg "Python test for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"

# The test command will usually depend upon what is contained in the tox.ini file
# or in the [testenv:py] section of the pyproject.toml file.
  PYTHONPATH=. ${MINGW_PREFIX}/bin/python -m pytest || warning "Tests failed"
}

package() {
  msg "Python install for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
